#
# $Id: SConscript,v 1.5 2011/11/04 00:31:33 kutschke Exp $
# $Author: kutschke $
# $Date: 2011/11/04 00:31:33 $
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)



if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'art_Framework_Core',
                             'GeometryService_service',
                             'Mu2eUtilities',
                             'CLHEP',
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]




for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                     LIBS=[ libs
                            ],
                       )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
