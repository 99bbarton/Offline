#
# $Id: SConscript.sav,v 1.3 2011/05/17 16:30:15 greenc Exp $
# $Author: greenc $
# $Date: 2011/05/17 16:30:15 $
#
# Original author Rob Kutschke.
#

import os, re
home = os.environ['FW_HOME']
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)
    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'FWCoreFramework',
                             'BaBar_KalmanTrack',
                             'BaBar_DetectorModel',
                             'BaBar_TrkBase',
                             'BaBar_BField',
                             'BaBar_TrajGeom',
                             'BaBar_BbrGeom',
                             'BaBar_difAlgebra',
                             'BaBar_ProbTools',
                             'BaBar_BaBar',
                             'BaBar_CLHEP',
                             'ToyDP',
                             'GeometryService',
                             'GeneralUtilities',
                             'CLHEP',
                           ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                     LIBS=[ 'FWCoreFramework', libs, 'ToyDP', 'GeometryService',
                            'GeneralUtilities', 'CLHEP',
                          ],
                     )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
