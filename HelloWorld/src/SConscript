#
# Script to bulid the files found in this directory.
#
# $Id: SConscript,v 1.2 2010/05/18 21:47:46 kutschke Exp $
# $Author: kutschke $
# $Date: 2010/05/18 21:47:46 $
#
# Original author Rob Kutschke.
#

import os, re
home = os.environ['MU2E_HOME']
Import('env')

plugin_cc = Glob('*_plugin.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'FWCoreFramework',
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=[libs, ] )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
