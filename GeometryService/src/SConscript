#
# $Id: SConscript,v 1.10 2011/05/18 19:18:10 greenc Exp $
# $Author: greenc $
# $Date: 2011/05/18 19:18:10 $
#
# Original author Rob Kutschke.
#

import os.path
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc',strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

libname='#/lib/GeometryService'

env._dict["LIBPATH"] += ['#/lib']
env.SharedLibrary(libname, non_plugin_cc,
        LIBS=[ 'Mu2eUtilities',
               'CTrackerGeom',
               'LTrackerGeom',
               'TTrackerGeom',
               'ITrackerGeom',
               'TrackerGeom',
               'TargetGeom',
               'BeamlineGeom',
               'VirtualDetectorGeom',
               'CalorimeterGeom',
               'BFieldGeom',
               'CosmicRayShieldGeom',
               'art_Persistency_Provenance',
               'art_Persistency_Common',
               'art_Framework_Services_Registry',
               'art_Framework_IO_Catalog',
               'MF_MessageLogger',
               'fhiclcpp',
               'art_Utilities',
               'Core',
               'Hist',
               'Tree',
               'RIO',
               'Net',
               'Reflex',
               'Cintex',
             ],
)


libnm=os.path.basename(libname)
for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=[ libnm ] )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
