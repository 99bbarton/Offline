#
# $Id: SConscript,v 1.22 2012/07/15 22:06:17 kutschke Exp $
# $Author: kutschke $
# $Date: 2012/07/15 22:06:17 $
#
# Original author Rob Kutschke.
#

import os.path
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc',strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

local_lib_name='GeometryService'

env._dict["LIBPATH"] += ['#/lib']
env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
        LIBS=[ 
               'BFieldGeom',
               'ExtinctionMonitorUCIGeom',
               'ExtinctionMonitorFNAL',
               'CosmicRayShieldGeom',
               'CalorimeterGeom',
               'LTrackerGeom',
               'TTrackerGeom',
               'ITrackerGeom',
               'TrackerGeom',
               'TargetGeom',
               'Mu2eBuildingGeom',
               'ProtonBeamDumpGeom',
               'ProductionTargetGeom',
               'ProductionSolenoidGeom',
               'BeamlineGeom',
               'MECOStyleProtonAbsorberGeom',
               'MBSGeom',
               'GeomPrimitives',
               'Mu2eUtilities',
               'ConfigTools',
               'Mu2eInterfaces',
               'art_Framework_Core',
               'art_Framework_Services_Registry',
               'MF_MessageLogger',
               'fhiclcpp',
               'art_Utilities',
               'Core',
               'Hist',
               'Tree',
               'RIO',
               'Net',
               'Reflex',
               'Cintex',
             ],
)

env.DictionarySource(['#/tmp/src/GeometryService/src/%s_dict.cpp'%(local_lib_name),
                      '#/tmp/src/GeometryService/src/%s_map.cpp'%(local_lib_name)],
                     ['classes.h','classes_def.xml'])

env.SharedLibrary('#/lib/%s_dict'%(local_lib_name,),
                  '#/tmp/src/GeometryService/src/%s_dict.cpp'%(local_lib_name),
                  LIBS=[local_lib_name, 'CLHEP'])

env.SharedLibrary('#/lib/%s_map'%(local_lib_name,),
                  '#/tmp/src/GeometryService/src/%s_map.cpp'%(local_lib_name))


#================================================================
for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=[ local_lib_name ] )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
