#
# $Id: SConscript,v 1.1 2009/09/30 22:57:47 kutschke Exp $
# $Author: kutschke $
# $Date: 2009/09/30 22:57:47 $
#
# Original author Rob Kutschke.
#

import os.path, re
Import('env')

# last two components of the path. Ex: /not/this/but/THIS/AND_THIS
#                                      "AND_THIS" is usually "src"
dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
ff=os.path.dirname(dd)   # THIS
env.DictionarySource(['#/tmp/src/%s/%s_dict_plugin.cpp'%(dd,ff),
                      '#/tmp/src/%s/%s_map_plugin.cpp'%(dd,ff)],
                     ['classes.h','classes_def.xml'])

my_lib=[]  # default if no cc_files
cc_files = Glob('*.cc',strings=True)
if cc_files:
    env.SharedLibrary( '#/lib/%s'%(ff,), cc_files,
                       # common libs
                       LIBS=[ 'FWCoreUtilities',
                              'FWCoreServiceRegistry',
                              'boost_thread-gcc%s-mt'%(env.gcc_ver,),
                              'Reflex',
                              'Core',
                            ],
        )
    my_lib=['%s'%(ff,),'CLHEP']

env.SharedLibrary('#/lib/%s_dict_plugin'%(ff,),
                  '#/tmp/src/%s/%s_dict_plugin.cpp'%(dd,ff),
                  LIBS=my_lib)
env.SharedLibrary('#/lib/%s_map_plugin'%(ff,),
                  '#/tmp/src/%s/%s_map_plugin.cpp'%(dd,ff))


