#
# $Id: SConscript,v 1.3 2011/05/18 19:18:10 greenc Exp $
# $Author: greenc $
# $Date: 2011/05/18 19:18:10 $
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'art_Framework_Core',
                             'ToyDP',
                             'Mu2eG4',
                             'GeometryService',
                             'GeneralUtilities',
                             'CLHEP',
                             'CDFTrajectory',
                             'Minuit'
                           ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                     LIBS=[ 'art_Framework_Core', libs, 'ToyDP','Mu2eG4', 'GeometryService',
                            'GeneralUtilities', 'CLHEP','CDFTrajectory','Minuit'
                          ],
                     )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
