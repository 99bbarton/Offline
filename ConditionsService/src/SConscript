# $Id: SConscript,v 1.11 2012/07/15 22:06:17 kutschke Exp $
# $Author: kutschke $
# $Date: 2012/07/15 22:06:17 $
#
# Original author Rob Kutschke.
#

import os.path
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc',strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

libname='#/lib/ConditionsService'

env._dict["LIBPATH"] += ['#/lib']
env.SharedLibrary(libname, non_plugin_cc,
        LIBS=[ 'Mu2eInterfaces',
               'Mu2eUtilities',
               'ConfigTools',
               'HepPDT',
               'HepPID',
               'MF_MessageLogger',
               'art_Framework_Core',
               'art_Framework_Services_Registry'
               ],
        )


libnm=os.path.basename(libname)
for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=[ 'art_Framework_Core',
                                           libnm,
                                           ] )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
