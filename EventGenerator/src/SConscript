#
# $Id: SConscript,v 1.8 2010/06/15 16:51:36 kutschke Exp $
# $Author: kutschke $
# $Date: 2010/06/15 16:51:36 $
#
# Original author Rob Kutschke.
#

import os, re
home = os.environ['FRAMEWORK_DIR']
Import('env')

env.Append(FORTRANFLAGS = '-fno-automatic -finit-local-zero -ffortran-bounds-check' )

plugin_cc = Glob('*_plugin.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)
fortran = Glob('*.f', strings=True)

cc_and_f = [ non_plugin_cc, fortran ]

if cc_and_f:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, cc_and_f,
                      LIBS=[ 'FWCoreFramework',
                             'ToyDP',
                             'CLHEP',
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=libs )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
