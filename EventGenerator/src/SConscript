#
# $Id: SConscript,v 1.21 2012/07/15 22:06:17 kutschke Exp $
# $Author: kutschke $
# $Date: 2012/07/15 22:06:17 $
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

env.Append(FORTRANFLAGS = '-fno-automatic -finit-local-zero -fcheck=bounds' )

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)
fortran = Glob('*.f', strings=True)

cc_and_f = [ non_plugin_cc, fortran ]

if cc_and_f:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, cc_and_f,
                      LIBS=[ 'art_Framework_Services_Optional_TFileService_service',
                             'GeometryService',
                             'ConditionsService',
                             'ExtinctionMonitorFNAL',
                             'SeedService_service',
                             'Mu2eUtilities',
                             'ConfigTools',
                             'MCDataProducts',
                             'CLHEP',
                             'art_Framework_Services_Optional_RandomNumberGenerator_service',
                             'art_Framework_Core',
                             'art_Framework_Modules_RandomNumberSaver_module.so'
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=[libs,
                                          'art_Framework_Services_Optional_TFileService_service',
                                          'GeometryService',
                                          'ConditionsService',
                                          'SeedService_service',
                                          'MCDataProducts',
                                          'ConfigTools',
                                          'CLHEP',
                                          'art_Framework_Services_Optional_RandomNumberGenerator_service',
                                          'art_Framework_Core',
                                          'art_Framework_Modules_RandomNumberSaver_module.so'
                                          ]
                                          )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
