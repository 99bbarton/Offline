#
# $Id: SConscript,v 1.4 2011/02/04 20:24:56 kutschke Exp $
# $Author: kutschke $
# $Date: 2011/02/04 20:24:56 $
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

plugin_cc = Glob('*_plugin.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'FWCoreFramework',
                             'GeometryService',
                             'ConditionsService',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'ToyDP',
                             'Spectrum',
                             'MLP',
                             'Gpad',
                             'Graf'
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=['GeneralUtilities']

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                       LIBS=[ libs,
                             'FWCoreFramework',
                             'GeometryService',
                             'ConditionsService',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'ToyDP',
                             'Spectrum',
                             'MLP',
                             'Gpad',
                             'Graf' ], )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
