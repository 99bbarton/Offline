#
# $Id: SConscript,v 1.8 2011/05/24 17:19:03 kutschke Exp $
# $Author: kutschke $
# $Date: 2011/05/24 17:19:03 $
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'art_Framework_Core',
                             'art_Framework_Services_Optional_TFileService_service',
                             'GeometryService',
                             'ConditionsService',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'MCDataProducts',
                             'RecoDataProducts',
                             'CLHEP',
                             'Spectrum',
                             'MLP',
                             'Gpad',
                             'Graf'
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=['GeneralUtilities']

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                       LIBS=[ libs,
                             'art_Framework_Core',
                             'art_Framework_Services_Optional_TFileService_service',
                             'GeometryService',
                             'ConditionsService',
                             'MCDataProducts',
                             'RecoDataProducts',
                             'CLHEP',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'Spectrum',
                             'MLP',
                             'Gpad',
                             'Graf' ], )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
