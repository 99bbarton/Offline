#
# $Id: SConscript,v 1.1 2012/08/24 15:08:29 gandr Exp $
# $Author: gandr $
# $Date: 2012/08/24 15:08:29 $
#
# Original author Rob Kutschke.
#

import os.path, re
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

# Construct library name using its path relative to Offline
local_lib_path = re.sub(r'^.*/Offline/(.*)/src$', r'\1', env.Dir('.').abspath)
local_lib_name=re.sub(r'/', r'_', local_lib_path)

my_libs=['CLHEP']  # default if no cc_files
if non_plugin_cc:
    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'Mu2eInterfaces',
                             'Mu2eUtilities',
                             'ConfigTools',
                             'ProtonBeamDumpGeom',
                             'CLHEP'
                             ],
                      )
    my_libs=[local_lib_name, 'CLHEP']
    pass

for cc in plugin_cc:
    libname='#/lib/'+local_lib_name+'_'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=[ my_libs ] )


# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
