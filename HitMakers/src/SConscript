#
# $Id: SConscript,v 1.17 2012/02/08 16:46:30 kutschke Exp $
# $Author: kutschke $
# $Date: 2012/02/08 16:46:30 $
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'art_Framework_Core',
                             'art_Framework_Modules_RandomNumberSaver_module',
                             'art_Framework_Services_Optional_TFileService_service',
                             'GeometryService',
                             'ConditionsService_service',
                             'SeedService_service',
                             'CDFTrajectory',
                             'MCDataProducts',
                             'RecoDataProducts',
                             'GeneralUtilities',
                             'CLHEP',
                             'Minuit'
                           ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                     LIBS=[ 'art_Framework_Core',
                            'art_Framework_Modules_RandomNumberSaver_module',
                            'art_Framework_Services_Optional_TFileService_service',
                            libs,
                            'GeometryService',
                            'ConditionsService_service',
                            'SeedService_service',
                            'CDFTrajectory',
                            'MCDataProducts',
                            'RecoDataProducts',
                            'GeneralUtilities',
                            'CLHEP',
                            'Minuit'
                          ],
                     )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
