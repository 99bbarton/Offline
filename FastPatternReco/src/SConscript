#
# $Id: SConscript,v 1.4 2012/07/22 01:06:02 kutschke Exp $
# $Author: kutschke $
# $Date: 2012/07/22 01:06:02 $
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

# This comes from: root-config --cflags --glibs
# Then guess at the correct location of Spectrum and MLP.
rootlibs = [ 'Core', 'Cint', 'RIO', 'Net', 'Hist', 'Spectrum', 'MLP', 'Graf', 'Graf3d', 'Gpad', 'Tree',
             'Rint', 'Postscript', 'Matrix', 'Physics', 'MathCore', 'Thread', 'Gui', 'm', 'dl' ]

babarlibs = env['BABARLIBS']

plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'art_Framework_Core',
                             'art_Framework_Services_Optional_TFileService_service',
                              babarlibs,
                             'GeometryService',
                             'ConditionsService',
                             'SeedService_service',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'MCDataProducts',
                             'RecoDataProducts',
                             'TrkPatRec',
                             'KalmanTests',
                             'CLHEP',
                             rootlibs
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=['GeneralUtilities']

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                       LIBS=[ libs,
                             'art_Framework_Core',
                             'art_Framework_Services_Optional_TFileService_service',
                             'GeometryService',
                             'ConditionsService',
                             'SeedService_service',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'MCDataProducts',
                             'RecoDataProducts',
                             'TrkPatRec',
                             'KalmanTests',
                             'CLHEP',
                              rootlibs
                              ], )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
