#
# $Id: SConscript,v 1.3 2011/10/13 19:22:30 kutschke Exp $
# $Author: kutschke $
# $Date: 2011/10/13 19:22:30 $
#
# Original author David Brown, LBNL
#

import os, re
Import('env')

# This comes from: root-config --cflags --glibs
# Then guess at the correct location of Spectrum and MLP.
#rootlibs = [ 'Core', 'Cint', 'RIO', 'Net', 'Hist', 'Spectrum', 'MLP', 'Graf', 'Graf3d', 'Gpad', 'Tree',
#             'Rint', 'Postscript', 'Matrix', 'Physics', 'MathCore', 'Thread', 'Gui', 'm', 'dl' ]

rootlibs = ['Spectrum' ]

babarlibs = [ 'BaBar_KalmanTrack', 'BaBar_DetectorModel',  'BaBar_TrkBase',    'BaBar_BField',
              'BaBar_TrajGeom',    'BaBar_BbrGeom',        'BaBar_difAlgebra', 'BaBar_ProbTools',
              'BaBar_BaBar',       'BaBar_CLHEP',          'BaBar_MatEnv' ]


plugin_cc = Glob('*_module.cc', strings=True) + Glob('*_service.cc', strings=True) + Glob('*_source.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)
  
    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'art_Framework_Core',
                             'art_Framework_Services_Optional_TFileService_service',
                              babarlibs,
                             'GeometryService',
                             'ConditionsService',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'MCDataProducts',
                             'RecoDataProducts',
                             'KalmanTests',
                             'CLHEP',
                             rootlibs
                             ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                       LIBS=[ libs,
                             'art_Framework_Core',
                             'art_Framework_Services_Optional_TFileService_service',
                              babarlibs,
                             'GeometryService',
                             'ConditionsService',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'MCDataProducts',
                             'RecoDataProducts',
                             'KalmanTests',
                             'CLHEP',
                              rootlibs
                              ], )

# last two components of the path. Ex: /not/this/but/THIS/AND_THIS
#                                      "AND_THIS" is usually "src"
dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
ff=os.path.dirname(dd)   # THIS

env.DictionarySource(['#/tmp/src/%s/%s_dict.cpp'%(dd,ff),
                      '#/tmp/src/%s/%s_map.cpp'%(dd,ff)],
                     ['classes.h','classes_def.xml'])

env.SharedLibrary('#/lib/%s_dict'%(ff,),
                  '#/tmp/src/%s/%s_dict.cpp'%(dd,ff),
                  LIBS=libs)
env.SharedLibrary('#/lib/%s_map'%(ff,),
                  '#/tmp/src/%s/%s_map.cpp'%(dd,ff))

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
